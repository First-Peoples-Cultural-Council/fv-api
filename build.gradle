buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.gretty:gretty:+'
  }
}

plugins {
  id 'groovy'
  id 'java'
  id "io.swagger.core.v3.swagger-gradle-plugin" version "2.0.10"
}

apply plugin: 'org.gretty'
apply plugin: 'idea'
apply plugin: 'war'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  jcenter()
  maven {
    url "http://maven.nuxeo.com/nexus/content/repositories/public-releases/"
  }
}

sourceSets {
  main {
    java {
      srcDirs = []
    }
    groovy {
      srcDirs = ['src/main/groovy', 'src/main/java']
    }
  }
}

gretty {
  httpPort = 8080
  contextPath = '/'
  servletContainer = 'tomcat9'
  loggingLevel = 'WARN'
  fileLogEnabled = false

  jvmArgs = [
      "-noverify",
      "-server",
      "-Xmx512m",
  ]
}


dependencies {
  implementation 'org.codehaus.groovy:groovy-all:2.5.+'

  compileOnly  'io.swagger.core.v3:swagger-jaxrs2:2.0.10'
  compileOnly  'io.swagger.core.v3:swagger-annotations:2.0.10'
  compileOnly  'io.swagger.core.v3:swagger-jaxrs2-servlet-initializer:2.0.10'

  implementation 'org.glassfish.jersey.core:jersey-server:2.29'
  implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:2.29'
  implementation 'org.glassfish.jersey.containers:jersey-container-servlet:2.29'
  implementation 'org.glassfish.jersey.inject:jersey-hk2:2.29'
  implementation 'javax.activation:activation:1.1.1'
  implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
  implementation 'javax.xml.bind:jaxb-api:2.3.1'

  providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

  implementation 'ch.qos.logback:logback-classic:1.2.3'
  implementation 'com.amazonaws.serverless:aws-serverless-java-container-jersey:1.3.2' exclude group: "org.slf4j", module: "slf4j-api"


	implementation 'com.auth0:jwks-rsa:0.11.0'
  implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
  implementation 'io.jsonwebtoken:jjwt-jackson:0.11.1'
  implementation 'io.jsonwebtoken:jjwt-impl:0.11.1'

  implementation 'org.nuxeo.client:nuxeo-java-client:3.2.0'

  implementation 'com.google.inject:guice:4.2.2'
  implementation 'io.lettuce:lettuce-core:5.1.3.RELEASE'

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.glassfish.jersey.test-framework:jersey-test-framework-core:2.29'
  testImplementation 'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.29'
}

resolve {
  outputFileName = 'FirstVoicesAPI'
  outputFormat = 'jsonandyaml'
  prettyPrint = 'TRUE'
  classpath = sourceSets.main.runtimeClasspath
  //filterClass = 'firstvoices.aws.GroovyFilter'
  ignoredRoutes = ['/application.wadl']
  outputPath = 'openapi'
  dependsOn ':compileGroovy'
}

task buildZip(type: Zip) {
  from compileJava
  from compileGroovy
  from processResources
  into('lib') {
    from configurations.runtimeClasspath
  }
}

build.dependsOn buildZip

test {
    testLogging.showStandardStreams = true
}

